plugins {
    id 'org.springframework.boot' version '3.4.2'
    id 'org.openapi.generator' version '7.12.0'
}

group 'com.jinloes'
version '1.0-SNAPSHOT'

dependencies {
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'

    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'io.swagger.core.v3:swagger-core:2.2.28'
    implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
    implementation group: 'jakarta.annotation', name: 'jakarta.annotation-api', version: '3.0.0'
    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$rootProject.projectDir/openapi/src/main/openapi/bank_api.yaml"
    outputDir = "$projectDir/build/generated"
    apiPackage = "com.jinloes.openapi.web.api"
    invokerPackage = "com.jinloes.openapi.web.invoker"
    modelPackage = "com.jinloes.openapi.web.model"
    generateApiTests = true
    generateModelTests = true
    configOptions = [
            "delegatePattern"      : "false",
            "documentationProvider": "none",
            "interfaceOnly"        : "true",
            "library"              : "spring-boot",
            "useSpringBoot3"       : "true"
    ]
}

sourceSets {
    main {
        java {
            // TODO: Set this path according to what was generated for you
            srcDir("$buildDir/generated/src/main/java")
        }
    }
}

tasks.compileJava {
    dependsOn(tasks.openApiGenerate)
}

test {
    useJUnitPlatform()
}